<?xml version="1.0" encoding="utf-8"?>
<ui:DraggablePanel xmlns:mx="http://www.adobe.com/2006/mxml"
				   xmlns:ui="weave.ui.*"
				   xmlns:editors="weave.editors.*"
				   layout="absolute" width="25%" height="20%">
	<mx:Script>
		<![CDATA[
			import mx.controls.Button;
			import mx.core.UIComponent;
			
			import weave.Weave;
			import weave.api.getCallbackCollection;
			import weave.data.KeySets.KeySet;
			import weave.utils.LinkableTextFormat;
			import weave.utils.ProbeTextUtils;
			
			override protected function constructor():void
			{
				super.constructor();
				
				var ltf:LinkableTextFormat = Weave.properties.defaultTextFormat;
				var target:UIComponent = this;
				getCallbackCollection(ltf).addGroupedCallback(this, function():void { ltf.copyToStyle(target); }, true);
			}
			
			override protected function get defaultPanelTitle():String
			{
				return lang("Probe Information");
			}
			
			private var probeKeySet:KeySet = null;
			
			override protected function createChildren():void
			{
				super.createChildren();
				var button:Button = new Button();
				button.label = lang("Open probe tool tip editor");
				button.addEventListener(MouseEvent.CLICK, openToolTipEditor);
				settingsEditor.addChild(button); 
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				probeKeySet = Weave.root.getObject(Weave.DEFAULT_PROBE_KEYSET) as KeySet;
				probeKeySet.addImmediateCallback(this, updateText);
			}
			
			private function updateText():void
			{
				if( probeKeySet.keys.length > 0 )
					probeTextArea.text = ProbeTextUtils.getProbeText(probeKeySet.keys);
				else
					probeTextArea.text = "";
			}
			
			protected function openToolTipEditor(event:MouseEvent):void
			{
				DraggablePanel.openStaticInstance(ProbeToolTipEditor);
			}
			
		]]>
	</mx:Script>
	<mx:TextArea id="probeTextArea" height="100%" width="100%" editable="false"/>
	<ui:ControlPanel>
		<editors:WindowSettingsEditor id="settingsEditor" target="{this}" />
	</ui:ControlPanel>
</ui:DraggablePanel>
