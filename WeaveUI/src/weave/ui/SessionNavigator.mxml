<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<!--	Sesisoned Column References Navigator
*  @author skolman
*  
-->
<ui:CustomTree xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:ui="weave.ui.*"
		 width="100%" height="100%" 
		 disclosureOpenIcon="@Embed(source='Assets.swf',symbol='TreeDisclosureOpen')" 
		 disclosureClosedIcon="@Embed(source='Assets.swf',symbol='TreeDisclosureClosed')"
		 
		 change="handleItemSelect(event)"
		 openDuration="0"
		 doubleClickEnabled="true"
		 doubleClick="handleDoubleClick()"
		 labelFunction="{myLabelFunction}"
		 >
	
	<mx:Script>
		<![CDATA[
			import mx.events.ListEvent;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableObject;
			import weave.core.SessionManager;

			private var _rootObject:ILinkableObject = null;
			public function set rootObject(value:ILinkableObject):void
			{
				if (_rootObject == value)
					return;
				
				_rootObject = value;
				
				if (value == Weave.root)
					_linkableObjectName = "Weave";
				else if (Weave.root.getName(_rootObject) != null)
					_linkableObjectName = Weave.root.getName(_rootObject);
				else
					_linkableObjectName = null;
				
				verticalScrollPosition = 0;
				handleRootObjectChange();	
			}
			public function get rootObject():ILinkableObject
			{
				return _rootObject;
			}
			
			private var _linkableObjectName:String = "Weave";
			override protected function childrenCreated():void
			{
				super.childrenCreated();

				if(rootObject == null)
					rootObject = Weave.root;
				
//				this.dataProvider = getTree(rootObject,_linkableObjectName,_sessionObjectTypeFilter);
//				this.dataProvider.refresh();
				
				refresh(rootObject);
			}
			
			private function myLabelFunction(item:Object):String
			{
				// append class name to the label.
				var label:String = getQualifiedClassName(item.object).split("::")[1];
				if (item.label)
					label += ' "' + item.label + '"';
				return label;
			}
			
			private function handleRootObjectChange():void
			{
				//trace("handleRootObjectChange");
//				var tree:Object = getTree(rootObject,_linkableObjectName,_sessionObjectTypeFilter);
//				this.dataProvider = tree;
//				this.selectedItem = tree;
//				this.dataProvider.refresh();

				refresh(rootObject);
			}
			
			private function handleItemSelect(event:ListEvent):void
			{
				//trace(event.currentTarget);
				expandItem(selectedItem, true);
			}
			
			private function handleDoubleClick():void
			{
				//expandItem(selectedItem, !isItemOpen(selectedItem))
				refresh();
				
				ControlPanel.openEditor(getSelectedLinkableObject());
			}
			
			private var _sessionObjectTypeFilter:Class = null;
			public function set sessionObjectTypeFilter(className:Class):void
			{
				_sessionObjectTypeFilter = className;
			
				var tree:Object = (WeaveAPI.SessionManager as SessionManager).getSessionStateTree(rootObject,_linkableObjectName,_sessionObjectTypeFilter);
				dataProvider = tree;
				dataProvider.refresh();
				
				refresh();
			}
			
			public function getSelectedLinkableObject():ILinkableObject
			{
				return selectedItem ? selectedItem.object : null;
			}
			
			public function getSelectedItemOfClassType():Object
			{
				if (_sessionObjectTypeFilter == null)
					return selectedItem;

				if (selectedItem && selectedItem.object is _sessionObjectTypeFilter)
					return selectedItem;

				return null;
			}
			
			public function refresh(selectedObject:ILinkableObject = null):void
			{
				var vsp:int = verticalScrollPosition;
				if (selectedObject == null)
					selectedObject = (selectedItem && selectedItem.object) as ILinkableObject;
				
				var prevExpandedItems:Array = openItems.concat();
				
				var rootItem:Object = (WeaveAPI.SessionManager as SessionManager).getSessionStateTree(rootObject, _linkableObjectName, _sessionObjectTypeFilter);
				this.dataProvider = rootItem
				this.dataProvider.refresh();
				validateNow();
				// expand previously-expanded items
				for each (var item:Object in prevExpandedItems)
					findCorrespondingNode(item.object, true);
				// select previously-selected item
				selectedItem = findCorrespondingNode(selectedObject, true);
				if (selectedIndex >= 0)
					scrollToIndex(selectedIndex);
				expandItem(rootItem, true);
				verticalScrollPosition = vsp;
			}
			
			// TODO: make this function iterative (breadth first search) to improve performance
			/**
			 * This function recursively checks subtree nodes for a matching sessioned object.
			 * @return The node object corresponding to the sessioned object, or null if no node was found.
			 */
			private function findCorrespondingNode(sessionedObject:ILinkableObject, expand:Boolean, subtree:Object = null):Object
			{
				if (subtree == null)
				{
					try
					{
						subtree = dataProvider[0];
					}
					catch (e:Error)
					{
						return null;
					}
				}
				if (subtree.object == sessionedObject)
				{
					if (expand)
						expandItem(subtree, true);
					return subtree;
				}
				if (subtree.children == null)
					return null;
				for each (var child:Object in subtree.children)
				{
					var result:Object = findCorrespondingNode(sessionedObject, expand, child);
					if (result != null)
					{
						/*if (expand)
							expandItem(subtree, true);*/
						return result;
					}
				}
				return null;
			}
			
			
			override public function expandItem(item:Object, open:Boolean, animate:Boolean=false, dispatchEvent:Boolean=false, cause:Event=null):void
			{			
				super.expandItem(item, open, animate, dispatchEvent, cause);

				// keep expanding children while there is only one child
				if (open && item && item.children && item.children.length == 1)
					expandItem(item.children[0], open, animate, dispatchEvent, cause);
			}
		]]>
	</mx:Script>

</ui:CustomTree>